plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}
repositories {
    maven { url = "https://maven.firstdarkdev.xyz/snapshots" }
    maven { url = "https://maven.terraformersmc.com/releases/" }
    maven { url = "https://maven.saps.dev/minecraft"; content { includeGroup "dev.latvian.mods" } }
    maven { url = "https://jitpack.io" }
    maven { url = "https://modmaven.dev" }
    maven { url = "https://maven.architectury.dev" }
    maven { url = "https://maven.tterrag.com" }
}
version = mod_version
group = mod_group_id
base { archivesName = mod_id + "-" + minecraft_version }
java.toolchain.languageVersion = JavaLanguageVersion.of(17)
minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true
    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods { "${mod_id}" { source sourceSets.main } }
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            jvmArg '-Xcheck:jni'
        }
        client { property 'forge.enabledGameTestNamespaces', mod_id }
        server { property 'forge.enabledGameTestNamespaces', mod_id; args '--nogui' }
    }
}
sourceSets.main.resources { srcDir 'src/generated/resources' }
dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    implementation fg.deobf("dev.toma.configuration:configuration-forge-1.20.1:2.2.0")
    implementation fg.deobf("com.tterrag.registrate:Registrate:MC1.20-1.3.3")
    implementation fg.deobf("com.lowdragmc.ldlib:ldlib-forge-1.20.1:1.0.25.r")
    implementation fg.deobf("com.gregtechceu.gtceu:gtceu-1.20.1:1.3.0-a:slim")
}
tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version: forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors: mod_authors, mod_description: mod_description, gtceu_version: gtceu_version
    ]
    inputs.properties replaceProperties
    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) { expand replaceProperties + [project: project] }
}
tasks.named('jar', Jar).configure { finalizedBy 'reobfJar' }
tasks.withType(JavaCompile).configureEach { options.encoding = 'UTF-8' }
